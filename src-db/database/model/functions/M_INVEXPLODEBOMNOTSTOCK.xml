<?xml version="1.0"?>
  <database name="FUNCTION M_INVEXPLODEBOMNOTSTOCK">
    <function name="M_INVEXPLODEBOMNOTSTOCK" type="NULL">
      <parameter name="p_pinstance_id" type="VARCHAR" mode="in">
        <default/>
      </parameter>
      <parameter name="p_invoiceline_id" type="VARCHAR" mode="in">
        <default><![CDATA[null]]></default>
      </parameter>
      <body><![CDATA[/*************************************************************************
* The contents of this file are subject to the Openbravo  Public  License
* Version  1.1  (the  "License"),  being   the  Mozilla   Public  License
* Version 1.1  with a permitted attribution clause; you may not  use this
* file except in compliance with the License. You  may  obtain  a copy of
* the License at http://www.openbravo.com/legal/license.html
* Software distributed under the License  is  distributed  on  an "AS IS"
* basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
* License for the specific  language  governing  rights  and  limitations
* under the License.
* The Original Code is Openbravo ERP.
* The Initial Developer of the Original Code is Openbravo SLU
* All portions are Copyright (C) 2013-2019 Openbravo SLU
* All Rights Reserved.
* Contributor(s):  ______________________________________.
************************************************************************/
  -- Logistice
  v_explode CHAR(1);
  v_invoiceID VARCHAR2(32);
  v_PriceList_Version_ID VARCHAR2(32);
  v_Record_ID VARCHAR2(32);
  v_C_Tax_ID VARCHAR2(32) ;
  TYPE RECORD IS REF CURSOR;
  Cur_Parameter RECORD;
  Cur_MPriceListVersion RECORD;
  CUR_BOM_Line RECORD;
  CUR_BOM RECORD;
  v_Line NUMBER:=0;
  v_ResultStr VARCHAR2(2000):='';
  v_result NUMBER:= 1;  
  v_IsSOTrx CHAR(1);
  v_period_inv C_InvoiceLine.c_period_id%TYPE;
  v_isdeferred_inv C_InvoiceLine.IsDeferred%TYPE;
  v_defplantype_inv C_InvoiceLine.DefPlanType%TYPE;
  v_periodnumber_inv C_InvoiceLine.periodnumber%TYPE;
  v_BaseAmount VARCHAR2(32);
  v_invoiceline_ID VARCHAR2(32);


  BEGIN
  IF (p_PInstance_ID IS NOT NULL) THEN
      --  Update AD_PInstance
      DBMS_OUTPUT.PUT_LINE('Updating PInstance - Processing ' || p_PInstance_ID) ;
      v_ResultStr:='PInstanceNotFound';
      AD_UPDATE_PINSTANCE(p_PInstance_ID, NULL, 'Y', NULL, NULL) ;
      -- Get Parameters
      v_ResultStr:='ReadingParameters';
      FOR Cur_Parameter IN
        (SELECT i.Record_ID, i.AD_User_ID, p.ParameterName, p.P_String,
          p.P_Number, p.P_Date
        FROM AD_PINSTANCE i
        LEFT JOIN AD_PINSTANCE_PARA p
          ON i.AD_PInstance_ID=p.AD_PInstance_ID
        WHERE i.AD_PInstance_ID=p_PInstance_ID
        ORDER BY p.SeqNo
        )
      LOOP
        v_Record_ID:=Cur_Parameter.Record_ID;
      END LOOP; -- Get Parameter
  ELSE
      v_Record_ID:=p_invoiceline_ID;
  END IF;
    DBMS_OUTPUT.PUT_LINE('  Record_ID=' || v_Record_ID) ;
  BEGIN --BODY
     
    SELECT C_INVOICE.c_invoice_id, c_invoiceline.explode
      INTO v_invoiceID, v_explode
    FROM C_INVOICE JOIN c_invoiceline ON C_INVOICE.c_invoice_id = c_invoiceline.c_invoice_id
    WHERE c_invoiceline.c_invoiceline_id=v_Record_ID  FOR UPDATE;

    IF (v_explode = 'Y') THEN
      v_ResultStr:='@ProdExploded@';
      RAISE_APPLICATION_ERROR(-20000, '@ProdExploded@') ;
    END IF;
    
    v_ResultStr:='Exploding';


    -- Get Price List Version
    FOR Cur_MPriceListVersion IN
      (SELECT v.M_PriceList_Version_ID AS PriceList_Version_ID, o.M_PriceList_ID
       FROM M_PRICELIST_VERSION v, C_INVOICE o
       WHERE v.M_PriceList_ID=o.M_PriceList_ID
         AND v.ValidFrom<=COALESCE(o.DateOrdered, o.DateInvoiced)
         AND v.IsActive='Y'
         AND o.C_Invoice_ID=v_invoiceID
       ORDER BY v.ValidFrom DESC
      )
   LOOP
      v_PriceList_Version_ID:=Cur_MPriceListVersion.PriceList_Version_ID;
      EXIT;
    END LOOP;
 
    -- Replace Lines
    v_ResultStr:='Inserting lines';
        
    FOR CUR_BOM_Line IN
      (SELECT l.*,i.dateinvoiced,i.C_BPartner_Location_ID,i.issotrx
       FROM C_INVOICELINE l, c_invoice i
       WHERE l.C_invoiceline_ID=v_Record_ID
         AND l.c_invoice_id = i.c_invoice_id
         AND l.IsActive='Y'
         AND EXISTS
           (SELECT *
            FROM M_PRODUCT p
            WHERE l.M_Product_ID=p.M_Product_ID
              AND p.IsBOM='Y'
              AND p.IsStocked='N'
            )
       ORDER BY l.Line
       )
     LOOP
      v_Line:=CUR_BOM_Line.Line;
      -- Create New Lines
      FOR CUR_BOM IN
        (SELECT b.M_ProductBOM_ID, p.C_UOM_ID, b.BOMQty, b.Description,
           revplantype, isdeferredrevenue, periodnumber, defaultperiod,
           expplantype, isdeferredexpense, periodnumber_exp, defaultperiod_exp
         FROM M_PRODUCT_BOM b, M_PRODUCT p
             WHERE b.M_Product_ID=CUR_BOM_Line.M_Product_ID
               AND b.M_ProductBOM_ID=p.M_Product_ID
               AND b.isactive = 'Y'
             ORDER BY Line
         )
      LOOP
        v_Line:=v_Line + 1;
        
        IF(CUR_BOM.M_ProductBOM_ID IS NOT NULL) THEN
          SELECT issotrx 
          INTO v_IsSOTrx
          from c_invoice
          WHERE c_invoice_id = v_invoiceID;
          
          IF(v_IsSOTrx = 'Y') THEN
 	    v_period_inv:= c_get_default_period(CUR_BOM_Line.C_Invoice_ID, CUR_BOM.defaultperiod);
	    IF(v_period_inv IS NOT NULL AND CUR_BOM.isdeferredrevenue = 'Y') THEN
	      v_isdeferred_inv := CUR_BOM.isdeferredrevenue;
	      v_defplantype_inv := CUR_BOM.revplantype;
	      v_periodnumber_inv := CUR_BOM.periodnumber;
	    ELSE
	      v_period_inv := NULL;
	      v_isdeferred_inv := 'N';
	      v_defplantype_inv := NULL;
	      v_periodnumber_inv := NULL;
	    END IF;
	  ELSE
            v_period_inv:= c_get_default_period(CUR_BOM_Line.C_Invoice_ID, CUR_BOM.defaultperiod_exp);
            IF(v_period_inv IS NOT NULL AND CUR_BOM.isdeferredexpense = 'Y') THEN
              v_isdeferred_inv := CUR_BOM.isdeferredexpense;
              v_defplantype_inv := CUR_BOM.expplantype;
              v_periodnumber_inv := CUR_BOM.periodnumber_exp;
            ELSE
              v_period_inv := NULL;
              v_isdeferred_inv := 'N';
              v_defplantype_inv := NULL;
              v_periodnumber_inv := NULL;
            END IF;
          END IF;
        END IF;

        v_C_Tax_ID:=C_Gettax(CUR_BOM.M_ProductBOM_ID, CUR_BOM_Line.dateinvoiced, 
        CUR_BOM_Line.AD_Org_ID, null, CUR_BOM_Line.C_BPartner_Location_ID, 
        CUR_BOM_Line.C_BPartner_Location_ID, null, CUR_BOM_Line.issotrx) ;
       v_invoiceline_ID:=get_uuid();
 
        INSERT INTO C_INVOICELINE
          (
            C_InvoiceLine_ID, AD_Client_ID, AD_Org_ID, IsActive,
            Created, CreatedBy, Updated, UpdatedBy,
            C_Invoice_ID, Line, Description, M_Product_ID,
            C_UOM_ID, QtyInvoiced, PriceList,
            PriceActual,
            PriceLimit, LineNetAmt, C_Charge_ID, ChargeAmt,
            C_Tax_ID, PriceStd,
            isdeferred, defplantype, periodnumber, c_period_id,
            c_bpartner_id, c_project_id, c_projectline_id, a_asset_id,
            user1_id, user2_id, bom_parent_id
          )
        VALUES
          (
            v_invoiceline_ID, CUR_BOM_Line.AD_Client_ID, CUR_BOM_Line.AD_Org_ID, CUR_BOM_Line.IsActive,
            now(), '0', now(), '0',
            CUR_BOM_Line.C_Invoice_ID, v_Line, CUR_BOM.Description, CUR_BOM.M_ProductBOM_ID,
            CUR_BOM.C_UOM_ID, CUR_BOM_Line.QtyInvoiced*CUR_BOM.BOMQty, M_BOM_PriceList(CUR_BOM.M_ProductBOM_ID, v_PriceList_Version_ID),
            M_BOM_PriceStd(CUR_BOM.M_ProductBOM_ID, v_PriceList_Version_ID),
            M_BOM_PriceLimit(CUR_BOM.M_ProductBOM_ID, v_PriceList_Version_ID),
            M_BOM_PriceStd(CUR_BOM.M_ProductBOM_ID, v_PriceList_Version_ID) * CUR_BOM_Line.QtyInvoiced*CUR_BOM.BOMQty, CUR_BOM_Line.C_Charge_ID, CUR_BOM_Line.ChargeAmt,
            coalesce(v_C_Tax_ID,CUR_BOM_Line.c_tax_id), M_BOM_PriceStd(CUR_BOM.M_ProductBOM_ID, v_PriceList_Version_ID),
            v_isdeferred_inv, v_defplantype_inv, v_periodnumber_inv, v_period_inv,
            CUR_BOM_Line.c_bpartner_id, CUR_BOM_Line.c_project_id, CUR_BOM_Line.c_projectline_id, CUR_BOM_Line.a_asset_id,
            CUR_BOM_Line.user1_id, CUR_BOM_Line.user2_id,v_Record_ID
          );

         SELECT BASEAMOUNT
         INTO v_BaseAmount
         FROM C_TAX
         WHERE C_TAX_ID = v_C_Tax_ID;
         IF (v_BaseAmount='TBA' OR v_BaseAmount='TBATAX') THEN
           UPDATE C_INVOICELINE SET Taxbaseamt= M_BOM_PriceStd(CUR_BOM.M_ProductBOM_ID, v_PriceList_Version_ID) where c_invoiceline_id=v_invoiceline_ID;
         END IF;

      END LOOP; -- Create New Lines
    END LOOP; -- Replace Lines
    UPDATE c_invoiceline set explode='Y' where c_invoiceline_id = v_Record_ID;
    
    IF (p_PInstance_ID IS NOT NULL) THEN
      DBMS_OUTPUT.PUT_LINE('Updating PInstance - Finished ');
      AD_UPDATE_PINSTANCE(p_pinstance_id, NULL, 'N', v_result, '');
    END IF;
   
  END; --BODY
EXCEPTION
WHEN OTHERS THEN
 DBMS_OUTPUT.PUT_LINE(v_ResultStr) ;
 v_ResultStr:= '@ERROR=' || SQLERRM;
  DBMS_OUTPUT.PUT_LINE(v_ResultStr) ;
  IF(p_PInstance_ID IS NOT NULL) THEN
    ROLLBACK;
    AD_UPDATE_PINSTANCE(p_pinstance_id, NULL, 'N', 0, v_resultstr);
   END IF;
END M_INVEXPLODEBOMNOTSTOCK
]]></body>
    </function>
  </database>
