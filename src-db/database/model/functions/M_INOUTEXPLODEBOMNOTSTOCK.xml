<?xml version="1.0"?>
  <database name="FUNCTION M_INOUTEXPLODEBOMNOTSTOCK">
    <function name="M_INOUTEXPLODEBOMNOTSTOCK" type="NULL">
      <parameter name="p_pinstance_id" type="VARCHAR" mode="in">
        <default/>
      </parameter>
      <parameter name="p_inoutline_id" type="VARCHAR" mode="in">
        <default><![CDATA[null]]></default>
      </parameter>
      <body><![CDATA[/*************************************************************************
* The contents of this file are subject to the Openbravo  Public  License
* Version  1.1  (the  "License"),  being   the  Mozilla   Public  License
* Version 1.1  with a permitted attribution clause; you may not  use this
* file except in compliance with the License. You  may  obtain  a copy of
* the License at http://www.openbravo.com/legal/license.html
* Software distributed under the License  is  distributed  on  an "AS IS"
* basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
* License for the specific  language  governing  rights  and  limitations
* under the License.
* The Original Code is Openbravo ERP.
* The Initial Developer of the Original Code is Openbravo SLU
* All portions are Copyright (C) 2013-2019 Openbravo SLU
* All Rights Reserved.
* Contributor(s):  ______________________________________.
************************************************************************/
  -- Logistice
  v_explode CHAR(1);
  v_Record_ID VARCHAR2(32);
  TYPE RECORD IS REF CURSOR;
  Cur_Parameter RECORD;
  CUR_BOM_Line RECORD;
  CUR_BOM RECORD;
  v_Line NUMBER:=0;
  v_ResultStr VARCHAR2(2000):='';
  v_result NUMBER:= 1;  
  v_aum C_ORDERLINE.C_AUM%TYPE;
  v_aumqty NUMBER;
  v_uom_preference AD_PREFERENCE.VALUE%TYPE;
  
  BEGIN
  IF (p_PInstance_ID IS NOT NULL) THEN
      --  Update AD_PInstance
      DBMS_OUTPUT.PUT_LINE('Updating PInstance - Processing ' || p_PInstance_ID) ;
      v_ResultStr:='PInstanceNotFound';
      AD_UPDATE_PINSTANCE(p_PInstance_ID, NULL, 'Y', NULL, NULL) ;
      -- Get Parameters
      v_ResultStr:='ReadingParameters';
      FOR Cur_Parameter IN
        (SELECT i.Record_ID, i.AD_User_ID, p.ParameterName, p.P_String,
          p.P_Number, p.P_Date
        FROM AD_PINSTANCE i
        LEFT JOIN AD_PINSTANCE_PARA p
          ON i.AD_PInstance_ID=p.AD_PInstance_ID
        WHERE i.AD_PInstance_ID=p_PInstance_ID
        ORDER BY p.SeqNo
        )
      LOOP
        v_Record_ID:=Cur_Parameter.Record_ID;
      END LOOP; -- Get Parameter
  ELSE
      v_Record_ID:=p_inoutline_ID;
  END IF;
    DBMS_OUTPUT.PUT_LINE('  Record_ID=' || v_Record_ID) ;
  BEGIN --BODY
     
    SELECT m_inoutline.explode
      INTO v_explode
    FROM M_INOUT
    JOIN m_inoutline ON M_INOUT.m_inout_id = m_inoutline.m_inout_id
    WHERE m_inoutline.m_inoutline_id=v_Record_ID  FOR UPDATE;

    IF (v_explode = 'Y') THEN
      v_ResultStr:='@ProdExploded@';
      RAISE_APPLICATION_ERROR(-20000, '@ProdExploded@') ;
    END IF;
    
    v_ResultStr:='Exploding';

    -- Get AUM
    v_uom_preference := 'N';      
    BEGIN
      v_uom_preference := AD_GET_PREFERENCE_VALUE('UomManagement', 'Y', NULL, NULL, NULL, NULL, NULL);
    EXCEPTION
      WHEN OTHERS THEN
      -- Nothing to do, v_uom_preference = 'N'
      DBMS_OUTPUT.PUT_LINE('The preference UomManagement was not found') ;
    END;
 
    -- Replace Lines
    v_ResultStr:='Inserting lines';
    FOR CUR_BOM_Line IN
      (SELECT l.*, i.c_doctype_id as io_c_doctype_id
       FROM M_INOUTLINE l
       JOIN M_INOUT i ON (l.m_inout_id = i.m_inout_id)
       WHERE l.m_inoutline_ID=v_Record_ID
         AND l.IsActive='Y'
         AND EXISTS
           (SELECT *
            FROM M_PRODUCT p
            WHERE l.M_Product_ID=p.M_Product_ID
              AND p.IsBOM='Y'
              AND p.IsStocked='N'
            )
       ORDER BY l.Line
       )
    LOOP
      v_Line:=CUR_BOM_Line.Line;
      -- Create New Lines
      FOR CUR_BOM IN
        (SELECT b.M_ProductBOM_ID, p.C_UOM_ID, b.BOMQty, b.Description
         FROM M_PRODUCT_BOM b
              INNER JOIN M_PRODUCT p ON (b.M_ProductBOM_ID=p.M_Product_ID)
         WHERE b.M_Product_ID=CUR_BOM_Line.M_Product_ID
           AND b.isactive='Y'
         ORDER BY Line
         )
      LOOP
  
        v_Aum := NULL;
        v_AumQty := NULL;
        IF (v_uom_preference = 'Y') THEN
          v_aum := M_GET_DEFAULT_AUM_FOR_DOCUMENT(CUR_BOM.M_ProductBOM_ID, CUR_BOM_Line.io_c_doctype_id);
          v_aumqty := M_GET_CONVERTED_QTY(CUR_BOM.M_ProductBOM_ID, CUR_BOM_Line.movementqty, v_aum); 
        END IF;      

        v_Line:=v_Line + 1;
        INSERT
        INTO M_INOUTLINE
          (
            M_InOutLine_ID,M_InOut_ID, AD_Client_ID,AD_Org_ID,
            IsActive,Created,CreatedBy,Updated, UpdatedBy, 
            Line,Description, M_Product_ID,
            C_UOM_ID,MovementQty,
            IsInvoiced, QuantityOrder, M_PRODUCT_UOM_ID,
            A_ASSET_ID, C_PROJECT_ID, C_COSTCENTER_ID,
            USER1_ID, USER2_ID, C_BPARTNER_ID, bom_parent_id,
            c_aum, aumqty
          )
        VALUES
          (
            get_uuid(),CUR_BOM_Line.m_inout_ID, CUR_BOM_Line.AD_Client_ID, CUR_BOM_Line.AD_Org_ID, 
            CUR_BOM_Line.IsActive, now(), '0', now(), '0',
            v_Line, CUR_BOM.Description, CUR_BOM.M_ProductBOM_ID, 
            CUR_BOM.C_UOM_ID, CUR_BOM_Line.MovementQty*CUR_BOM.BOMQty, 
            'N', CUR_BOM_Line.QuantityOrder, CUR_BOM_Line.M_Product_UOM_ID,
            CUR_BOM_Line.a_asset_id, CUR_BOM_Line.c_project_id, CUR_BOM_Line.c_costcenter_id,
            CUR_BOM_Line.user1_id, CUR_BOM_Line.user2_id, CUR_BOM_Line.C_BPartner_ID, v_Record_ID, 
            v_aum, v_aumqty*CUR_BOM.BOMQty
          );
      END LOOP; -- Create New Lines
    END LOOP; -- Replace Lines
    UPDATE m_inoutline set explode='Y' where m_inoutline_id = v_Record_ID;
    
    IF (p_PInstance_ID IS NOT NULL) THEN
      DBMS_OUTPUT.PUT_LINE('Updating PInstance - Finished ');
      AD_UPDATE_PINSTANCE(p_pinstance_id, NULL, 'N', v_result, '');
    END IF;
   
  END; --BODY
EXCEPTION
WHEN OTHERS THEN
 DBMS_OUTPUT.PUT_LINE(v_ResultStr) ;
 v_ResultStr:= '@ERROR=' || SQLERRM;
  DBMS_OUTPUT.PUT_LINE(v_ResultStr) ;
  IF(p_PInstance_ID IS NOT NULL) THEN
    ROLLBACK;
    AD_UPDATE_PINSTANCE(p_pinstance_id, NULL, 'N', 0, v_resultstr);
   END IF;
END M_INOUTEXPLODEBOMNOTSTOCK
]]></body>
    </function>
  </database>
